config {
  type: "table",
  schema: dataform.projectConfig.vars.gold_ecommerce_bq,
  tags: ["gold", "ecommerce", "sales_fact"]
}

SELECT
    items.id AS item_pedido_id,
    items.order_id AS pedido_id,
    items.product_id,
    ord.user_id,

    ord.data_pedido,
    ord.ano_pedido,
    ord.mes_pedido,
    ord.status AS status_pedido,

    prod.product_name,
    prod.category AS product_category,
    prod.brand AS product_brand,

    1 AS quantidade_vendida,
    items.valor_total_item AS valor_total_vendido,
    prod.cost AS custo_total_item,
    (items.valor_total_item - prod.cost) AS margem_bruta_item

FROM
    ${ref("slv_ecommerce_order_items")} AS items

-- Decisão Técnica: Mudança de LEFT para INNER JOIN.
-- A tabela de fatos SÓ deve conter vendas reais. Um item de um pedido
-- cancelado (filtrado na slv_ecommerce_orders) não é um fato de venda.
-- O INNER JOIN garante que apenas itens de pedidos válidos sejam incluídos.
INNER JOIN ${ref("slv_ecommerce_orders")} AS ord
    ON items.order_id = ord.order_id

-- Mantemos o LEFT JOIN para produtos, pois podemos querer analisar
-- vendas de produtos que foram removidos do catálogo.
LEFT JOIN ${ref("slv_ecommerce_products")} AS prod
    ON items.product_id = prod.product_id